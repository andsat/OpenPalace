<?xml version="1.0" encoding="utf-8"?>

<!--
This file is part of OpenPalace.

OpenPalace is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OpenPalace is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with OpenPalace.  If not, see <http://www.gnu.org/licenses/>.
-->

<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/halo"
	clipAndEnableScrolling="true"
	creationComplete="handleCreationComplete()" xmlns:view="net.codecomposer.palace.view.*" xmlns:mx1="library://ns.adobe.com/flex/mx">
	
	<fx:Metadata>
		[Event(name="securityError", type="net.codecomposer.palace.event.PalaceSecurityErrorEvent")]
	</fx:Metadata>
		
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.CollectionEvent;
			import mx.events.CollectionEventKind;
			
			import net.codecomposer.palace.event.HotspotEvent;
			import net.codecomposer.palace.event.PalaceSecurityErrorEvent;
			import net.codecomposer.palace.model.PalaceCurrentRoom;
			import net.codecomposer.palace.model.PalaceHotspot;
			import net.codecomposer.palace.model.PalaceHotspotState;
			import net.codecomposer.palace.model.PalaceImageOverlay;
			
			[Bindable]
			public var hotSpots:ArrayCollection;
			[Bindable]
			public var room:PalaceCurrentRoom;
			[Bindable]
			public var mediaServer:String;
			
			private var viewsByHotSpot:Dictionary = new Dictionary();
			private var nameTagsByHotSpot:Dictionary = new Dictionary();
			
			
			private function handleCreationComplete():void {
				hotSpots.addEventListener(CollectionEvent.COLLECTION_CHANGE, handleHotSpotsChange);
			}
			
			public function getHotspotsUnderPoint(roomPoint:Point):Vector.<HotSpotSprite> {
				var hotSpots:Vector.<HotSpotSprite> = new Vector.<HotSpotSprite>();
				var globalPoint:Point = localToGlobal(roomPoint);
				for each (var sprite:HotSpotSprite in viewsByHotSpot) {
					if (sprite.hitTestPoint(globalPoint.x, globalPoint.y, true)) {
						hotSpots.push(sprite);
					}
				}
				return hotSpots;
			}
			
			public function hitTestHotSpot(hotSpot:PalaceHotspot, globalPoint:Point):Boolean {
				var view:HotSpotSprite = HotSpotSprite(viewsByHotSpot[hotSpot]);
				if (view) {
					return view.hitTestPoint(globalPoint.x, globalPoint.y, true);
				}
				return false;
			}
			
			private function handleHotSpotsChange(event:CollectionEvent):void {
				var hotSpot:PalaceHotspot;
				if (event.kind == CollectionEventKind.ADD) {
					for each (hotSpot in event.items) {
						addHotSpot(hotSpot);
					}
				}
				else if (event.kind == CollectionEventKind.REMOVE) {
					for each (hotSpot in event.items) {
						removeHotSpot(hotSpot);
					}
				}
				else if (event.kind == CollectionEventKind.RESET) {
					resetHotSpots();
				}
			}
			
			private function addHotSpot(hotSpot:PalaceHotspot):void {
				
				var hotSpotSprite:HotSpotSprite = new HotSpotSprite(hotSpot, true);
				hotspotCanvas.rawChildren.addChild(hotSpotSprite);
				viewsByHotSpot[hotSpot] = hotSpotSprite;
				
				if (hotSpot.showName) {
					var nameTag:HotSpotNameTag = new HotSpotNameTag();
					nameTag.hotSpot = hotSpot;
					nameTag.mouseEnabled = false;
					nameTagsByHotSpot[hotSpot] = nameTag;
					hotspotNames.addElement(nameTag);
				}
				
				hotSpot.addEventListener(HotspotEvent.STATE_CHANGED, handleHotspotStateChanged);
				hotSpot.addEventListener(HotspotEvent.OPACITY_CHANGED, handleHotspotOpacityChanged);
				
				for (var i:int = 0; i < hotSpot.states.length; i ++) {
					var isCurrentState:Boolean = Boolean(hotSpot.state == i);
					
					var state:PalaceHotspotState = PalaceHotspotState(hotSpot.states.getItemAt(i));
					var imageOverlay:PalaceImageOverlay = room.getSpotImageById(state.pictureId);
					
					if (imageOverlay && imageOverlay.filename != null) {
						var hotSpotImage:HotSpotImage = new HotSpotImage();
						hotSpotImage.addEventListener(SecurityErrorEvent.SECURITY_ERROR, handleSecurityError);
						hotSpotImage.state = state;
						hotSpotImage.hotspot = hotSpot;
						hotSpotImage.imageOverlay = imageOverlay;
						hotSpotImage.visible = isCurrentState;
						hotSpotImage.alpha = state.opacity;
						state.hotspotImage = hotSpotImage;
						imageGroup.addElement(hotSpotImage);
					}
				}

			}
			
			private function handleSecurityError(event:SecurityErrorEvent):void {
				var securityEvent:PalaceSecurityErrorEvent = new PalaceSecurityErrorEvent(PalaceSecurityErrorEvent.SECURITY_ERROR);
				dispatchEvent(securityEvent);
			}
			
			private function removeHotSpot(hotSpot:PalaceHotspot):void {
				
			}
			
			private function resetHotSpots():void {
				viewsByHotSpot = new Dictionary();
				nameTagsByHotSpot = new Dictionary();
				while (hotspotCanvas.rawChildren.numChildren > 0) {
					hotspotCanvas.rawChildren.removeChildAt(0);
				}
				imageGroup.removeAllElements();
				hotspotNames.removeAllElements();
			}
			
			private function handleHotspotStateChanged(event:HotspotEvent):void {
				var hotspot:PalaceHotspot = PalaceHotspot(event.target);
				if (hotspot.type == PalaceHotspot.TYPE_LOCKABLE_DOOR ||
				    hotspot.type == PalaceHotspot.TYPE_SHUTABLE_DOOR) {
			    	if (hotspot.state == PalaceHotspot.STATE_LOCKED) {
			    		PalaceSoundPlayer.getInstance().playDoorLock();
			    	}
			    	if (hotspot.state == PalaceHotspot.STATE_UNLOCKED) {
			    		PalaceSoundPlayer.getInstance().playDoorUnlock();
			    	}
			    }
				for (var i:int = 0; i < hotspot.states.length; i++) {
					var hotspotState:PalaceHotspotState = PalaceHotspotState(hotspot.states.getItemAt(i));
					if (hotspotState.hotspotImage != null) {
						hotspotState.hotspotImage.visible = Boolean(i == event.state);
					}
				}
			}
			
			private function handleHotspotOpacityChanged(event:HotspotEvent):void {
				var hotspot:PalaceHotspot = PalaceHotspot(event.target);
				var hotspotState:PalaceHotspotState = PalaceHotspotState(hotspot.states.getItemAt(event.state));
				if (hotspotState && hotspotState.hotspotImage) {
					hotspotState.hotspotImage.alpha = hotspotState.opacity;
				}
			}
			
		]]>
	</fx:Script>
	
	<s:layout>
		<s:BasicLayout />
	</s:layout>

	<s:Group id="imageGroup" top="0" left="0" bottom="0" right="0"
		mouseEnabled="false">
		<s:layout>
			<s:BasicLayout />
		</s:layout>
	</s:Group>

	<s:Group id="hotspotNames" top="0" left="0" bottom="0" right="0"
			 mouseEnabled="false">
		<s:layout>
			<s:BasicLayout />
		</s:layout>
	</s:Group>
	
	<mx1:Canvas id="hotspotCanvas" top="0" left="0" bottom="0" right="0" 
			   verticalScrollPolicy="off" horizontalScrollPolicy="off"/>
	
</s:Group>
